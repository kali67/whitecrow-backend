

buildscript {
    ext.kotlin_version = '1.3.21' // Required for Kotlin integration
    ext.spring_boot_version = '2.1.0.RELEASE'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }
    dependencies { //dependencies used to build script
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlin_version}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlin_version}")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: "kotlin-jpa" //generate default constructors
apply plugin: 'kotlin' // Required for Kotlin integration
apply plugin: "kotlin-spring" // https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
apply plugin: 'org.springframework.boot'
apply plugin: "kotlin-allopen"
apply plugin: 'io.spring.dependency-management'
apply plugin: 'kotlin-kapt'

configurations {
    ktlint
}

allOpen {
    annotation "javax.persistence.Entity"
    annotation "javax.persistence.MappedSuperclass"
    annotation "javax.persistence.Embeddable"
}

noArg {
    annotation("javax.persistence.Entity")
}

jar {
    manifest {
        attributes 'Main-Class': 'main.java.Application'
    }
}

repositories {
    mavenCentral()
    jcenter()
}


// dependencies used to build project
dependencies {
    //group id: artifact: version
    ktlint "com.github.shyiko:ktlint:0.31.0"
    kapt "org.hibernate:hibernate-jpamodelgen:5.2.16.Final"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.apache.tomcat:tomcat-dbcp:9.0.1"
    compile("org.postgresql:postgresql:42.2.1") {
        exclude group: 'org.slf4j'
    }
    // https://mvnrepository.com/artifact/org.springframework.security/spring-security-core
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '2.1.4.RELEASE'
    compile group: 'com.esotericsoftware.yamlbeans', name: 'yamlbeans', version: '1.13'
    implementation 'com.google.code.gson:gson:2.8.5'
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile "org.springframework:spring-orm:5.0.2.RELEASE"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // Required for Kotlin integration
    compile "org.hibernate:hibernate-core:5.2.16.Final"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more    
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}